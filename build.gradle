plugins {
	id "java"
	id "maven-publish"
}

repositories {
	mavenCentral()
	maven {
		url "https://maven.luis-st.net/libraries/"
	}
}

compileJava {
	options.compilerArgs << "-parameters"
}

compileTestJava {
	options.compilerArgs << "-parameters"
}

dependencies {
	// Local
	implementation "net.luis:LUtils:${LUtils}" // Utility
	// Google
	implementation "com.google.guava:guava:${GoogleGuava}" // Utility
	// Apache
	implementation "org.apache.logging.log4j:log4j-api:${Log4jAPI}" // Logging
	implementation "org.apache.logging.log4j:log4j-core:${Log4jCore}" // Logging
	implementation "org.apache.commons:commons-lang3:${ApacheLang}" // Utility
	// Netty
	implementation "io.netty:netty-all:${Netty}" // Network
	// Other
	implementation "org.jetbrains:annotations:${JetBrainsAnnotations}" // Annotations
}

tasks.register("run", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.netcore.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
}

tasks.register("testClient", JavaExec) {
	group = "test"
	classpath = sourceSets.test.runtimeClasspath
	getMainClass().set("net.luis.netcore.ClientTest")
}

tasks.register("testServer", JavaExec) {
	group = "test"
	classpath = sourceSets.test.runtimeClasspath
	getMainClass().set("net.luis.netcore.ServerTest")
}

java {
	withSourcesJar()
}

artifacts {
	archives sourcesJar
}

def username = ""
def password = ""
def file = file("./../credentials.properties")
if (file.exists()) {
	def properties = new Properties()
	file.withInputStream {properties.load(it)}
	username = properties.getProperty("username")
	password = properties.getProperty("password")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "LNetCore"
			version = "1.0.0"
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		if (!username.isBlank() && !password.isBlank()) {
			maven {
				url "https://maven.luis-st.net/libraries/"
				credentials.username = username
				credentials.password = password
			}
		} else {
			System.err.println("No credentials provided. Publishing to maven.luis-st.net not possible.")
		}
	}
}

jar {
	manifest {
		attributes([
				"Built-By"     : "Luis Staudt",
				"Multi-Release": "true"
		])
	}
}